//@version=6
indicator("EMA Cross", overlay=true)
// Simple EMA cross indicator with indication of crosses on the EMA line and at the chart bottom
//
// Messy but true!

// User input: EMA lengths and source
ema_01_len = input.int(9, title='Ema 1', minval=1)
ema_02_len = input.int(20, title='Ema 2', minval=1)
ema_03_len = input.int(50, title='Ema 3', minval=1)
ema_04_len = input.int(158, title='Ema 4', minval=1)
ema_src = input.source(close, title="Source")

// Calculate EMA values
ema_01 = ta.ema(ema_src, ema_01_len)
ema_02 = ta.ema(ema_src, ema_02_len)
ema_03 = ta.ema(ema_src, ema_03_len)
ema_04 = ta.ema(ema_src, ema_04_len)

// Plot EMAs on chart
plot(ema_01, title='ema_01', color=#00FF00, style=plot.style_line, linewidth=1)
plot(ema_02, title='ema_02', color=color.new(#ffc800, 0), style=plot.style_line, linewidth=1)

plot(ema_03, title='ema_03', color=#ff7300cd, style=plot.style_line, linewidth=1)
plot(ema_04, title='ema_04', color=#cb0000, style=plot.style_line, linewidth=1)


//Cross detect
cross_01_up = ta.crossover(ema_01, ema_02)
cross_01_down = ta.crossunder(ema_01, ema_02)
// When cross occurs, draw a mark at the EMA level
plot(cross_01_up or cross_01_down ? ema_01 : na, style=plot.style_cross, title="cross 01", linewidth=3, color=ema_01 > ema_02 ? color.green : color.red)
// When cross occurs, draw a cross with text at the bottom of the chart
plotchar(cross_01_up or cross_01_down ? ema_01 : na, title="cross_01", char="x", location=location.bottom, color=ema_01 > ema_02 ? color.green : color.red, text="Cross 01", textcolor=ema_01 > ema_02 ? color.green : color.red)
//Cross2 detect
cross_02_up = ta.crossover(ema_03, ema_04)
cross_02_down = ta.crossunder(ema_03, ema_04)
// When cross occurs, draw a mark at the EMA level
plot(cross_02_up or cross_02_down ? ema_03 : na, style=plot.style_cross, title="cross 02", linewidth=3, color=ema_03 > ema_04 ? color.green : color.red)
// When cross occurs, draw a cross with text at the bottom of the chart
plotchar(cross_02_up or cross_02_down ? ema_03 : na, title="cross_02", char="x", location=location.bottom, color=ema_03 > ema_04 ? color.green : color.red, text="Cross 02", textcolor=ema_03 > ema_04 ? color.green : color.red)

alertcondition(cross_01_up, title='cross_01_up', message='a simple script is not a reason to buy! ;)')
alertcondition(cross_01_down, title='cross_01_down', message='a simple script is not a reason to sell! ;)')

// Highlight background: green on cross-over, red on cross-under (only the bar where the cross occurs)
bgcolor(cross_01_up ? color.new(color.green, 85) : cross_01_down ? color.new(color.red, 85) : na)

// === Bollinger Bands Inputs ===
bb_length = input.int(20, minval=1, title="BB Length")
bb_mult = input.float(2.0, minval=0.001, maxval=50, title="BB StdDev")
bb_maType = input.string("SMA", "BB Basis MA Type", options = ["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"])
bb_src = input(close, title="BB Source")
bb_offset = input.int(0, "BB Offset", minval = -500, maxval = 500, display = display.data_window)

// === Bollinger Bands Calculation ===
bb_ma(source, length, _type) =>
    switch _type
        "SMA" => ta.sma(source, length)
        "EMA" => ta.ema(source, length)
        "SMMA (RMA)" => ta.rma(source, length)
        "WMA" => ta.wma(source, length)
        "VWMA" => ta.vwma(source, length)

bb_basis = bb_ma(bb_src, bb_length, bb_maType)
bb_dev = bb_mult * ta.stdev(bb_src, bb_length)
bb_upper = bb_basis + bb_dev
bb_lower = bb_basis - bb_dev

// === Plot Bollinger Bands ===
plot(bb_basis, "BB Basis", color=color.blue, offset=bb_offset)
bb_p1 = plot(bb_upper, "BB Upper", color=color.yellow, offset=bb_offset)
bb_p2 = plot(bb_lower, "BB Lower", color=color.yellow, offset=bb_offset)
fill(bb_p2, bb_p1, title = "BB Background", color=color.rgb(91, 152, 212, 75))
